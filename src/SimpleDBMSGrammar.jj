options
{
  static = true;
  DEBUG_PARSER = false;
}

PARSER_BEGIN(SimpleDBMSParser)

import java.util.ArrayList;

public class SimpleDBMSParser
{
  private static SchemaManager sm = new SchemaManager();

  public static void main(String args []) throws ParseException
  {
    SimpleDBMSParser parser = new SimpleDBMSParser(System.in);
    while (true)
    {
      try
      {
        Message.print(Message.EMPTY);
        parser.command();
      }
      catch (ParseException e)
      {
        if(e.currentToken != null)
          Message.print(Message.SYNTAX_ERROR, false);
        SimpleDBMSParser.ReInit(System.in);
      }
    }
  }
}

PARSER_END(SimpleDBMSParser)

SKIP : { " " | "\r" | "\t" | "\n" }

TOKEN : /* Keywords */
{
  < EXIT : "exit" >
| < INT : "int" >
| < CHAR : "char" >
| < DATE : "date" >
| < CREATE_TABLE : "create table" >
| < DROP_TABLE : "drop table" >
| < DESC : "desc" >
| < SHOW_TABLES : "show tables" >
| < INSERT_INTO : "insert into" >
| < DELETE_FROM : "delete from" >
| < SELECT : "select" >
| < AS : "as" >
| < FROM : "from" >
| < NOT_NULL : "not null" >
| < PRIMARY_KEY : "primary key" >
| < FOREIGN_KEY : "foreign key" >
| < REFERENCES : "references" >
| < VALUES : "values" >
| < NULL : "null" >
| < WHERE : "where" >
| < OR : "or" >
| < AND : "and" >
| < NOT : "not" >
| < IS : "is" >
}

TOKEN :
{
  < SEMICOLON : ";" >
| < LEFT_PAREN : "(" >
| < RIGHT_PAREN : ")" >
| < ASTERISK : "*" >
| < QUOTE : "'" >
| < COMMA : "," >
| < PERIOD : "." >
| < UNDERSCORE : "_" >
| < SIGN : "+" | "-" >
| < INT_VALUE : (< SIGN >)? (< DIGIT >)+ >
| < DIGIT : [ "0"-"9" ] >
| < LEGAL_IDENTIFIER : < ALPHABET > (< ALPHABET > | < UNDERSCORE >)* >
| < ALPHABET : [ "A"-"Z", "a"-"z" ] >
| < CHAR_STRING : < QUOTE > (< NON_QUOTE_CHARACTER >)* < QUOTE > >
| < COMP_OP : "<" | ">" | "=" | ">=" | "<=" | "!=" >
| < NON_QUOTE_CHARACTER : < DIGIT > | < ALPHABET >
                        | < NON_QUOTE_SPECIAL_CHARACTERS > | " " >
| < NON_QUOTE_SPECIAL_CHARACTERS : [ "!", "#"-"&", "("-"/", ":"-"@", "["-"`", "{"-"~" ] >
| < DATE_VALUE : < NNNN > "-" < NN > "-" < NN > >
| < NNNN : < DIGIT > < DIGIT > < DIGIT > < DIGIT > >
| < NN : < DIGIT > < DIGIT > >
}

void command() :
{}
{
  queryList()
| (
    < EXIT >
    < SEMICOLON >
    {
      sm.close();
      System.exit(0);
    }
  )
}

void queryList() :
{
  Message msg;
}
{
  (
    msg = query()
    < SEMICOLON >
    {
      msg.print();
    }
  )+
}

Message query() :
{
  Message msg;
}
{
  (
    msg = createTableQuery()
    {
      return msg;
    }
  )
| (
    msg = dropTableQuery()
    {
      return msg;
    }
  )
| (
    descQuery()
    {
      return new Message(Message.EMPTY, false);
    }
  )
| (
    showTablesQuery()
    {
      return new Message(Message.SHOW_TABLES);
    }
  )
| (
    insertQuery()
    {
      return new Message(Message.INSERT);
    }
  )
| (
    deleteQuery()
    {
      return new Message(Message.DELETE);
    }
  )
| (
    selectQuery()
    {
      return new Message(Message.SELECT);
    }
  )
}

Message createTableQuery() :
{
  String s; Table t;
}
{
  < CREATE_TABLE >
  s = tableName()
  {
    if(sm.tableExists(s)) {
      Message.print(Message.TABLE_EXISTENCE_ERROR);
      throw new ParseException();
    }
    t = new Table(sm, s);
  }
  tableElementList(t)
  {
    t.save();
    return new Message(Message.CREATE_TABLE_SUCCESS, s);
  }
}

Message dropTableQuery() :
{
  ArrayList<String> l;
}
{
  < DROP_TABLE >
  l = tableNameList()
  {
    if(l == null) {
      sm.dropTables();
      return new Message(Message.DROP_SUCCESS_ALL_TABLES);
    }
    for(String tableName : l) {
      if(!sm.tableExists(tableName)) {
        Message.print(Message.NO_SUCH_TABLE);
        continue;
      }
      if(sm.getTable(tableName).isReferenced()) {
        Message.print(Message.DROP_REFERENCED_TABLE_ERROR, tableName);
        continue;
      }
      sm.dropTable(tableName);
      Message.print(Message.DROP_SUCCESS, tableName);
    }
    return new Message(Message.EMPTY, false);
  }
}

void descQuery() :
{
  ArrayList<String> l;
}
{
  < DESC >
  l = tableNameList()
  {
    if(l == null)
      l = sm.getTables();
    for(String tableName : l) {
      if(!sm.tableExists(tableName)) {
        Message.print(Message.NO_SUCH_TABLE);
        throw new ParseException();
      }
    }
    Message.print(Message.HORIZONTAL_LINE, false);
    for(String tableName : l) {
      sm.getTable(tableName).print();
      Message.print(Message.HORIZONTAL_LINE, false);
    }
  }
}

void showTablesQuery() :
{}
{
  < SHOW_TABLES >
}

void insertQuery() :
{}
{
  < INSERT_INTO >
  tableName()
  insertColumnsAndSource()
}

void deleteQuery() :
{}
{
  < DELETE_FROM >
  tableName()
  (
    whereClause()
  )?
}

void selectQuery() :
{}
{
  < SELECT >
  selectList()
  tableExpression()
}

void selectList() :
{}
{
  < ASTERISK >
| (
    selectedColumn()
    (
      < COMMA >
      selectedColumn()
    )*
  )
}

void selectedColumn() :
{}
{
  tableAndColumnName()
  (
    < AS >
    columnName()
  )?
}

void tableExpression() :
{}
{
  fromClause()
  (
    whereClause()
  )?
}

void fromClause() :
{}
{
  < FROM >
  tableReferenceList()
}

void tableReferenceList() :
{}
{
  referedTable()
  (
    < COMMA >
    referedTable()
  )*
}

void referedTable() :
{}
{
  tableName()
  (
    < AS >
    tableName()
  )?
}

void whereClause() :
{}
{
  < WHERE >
  booleanValueExpression()
}

// < BOOLEAN TERM > | < BOOLEAN VALUE EXPRESSION > or < BOOLEAN TERM >
// changed to
// < BOOLEAN TERM > [{ or < BOOLEAN TERM > }...]
// to remove left resursion
void booleanValueExpression() :
{}
{
  booleanTerm()
  (
    < OR >
    booleanTerm()
  )*
}

// < BOOLEAN FACTOR > | < BOOLEAN TERM > and < BOOLEAN FACTOR >
// changed to
// < BOOLEAN FACTOR > [{ and < BOOLEAN FACTOR > }...]
// to remove left resursion
void booleanTerm() :
{}
{
  booleanFactor()
  (
    < AND >
    booleanFactor()
  )*
}

void booleanFactor() :
{}
{
  (
    < NOT >
  )?
  booleanTest()
}

void booleanTest() :
{}
{
  predicate()
| parenthesizedBooleanExpression()
}

void parenthesizedBooleanExpression() :
{}
{
  < LEFT_PAREN >
  booleanValueExpression()
  < RIGHT_PAREN >
}

// < COMPARISON PREDICATE > | < NULL PREDICATE > have common prefixes of length 2
// so it changed to
//   (
//     < COMPARABLE VALUE > < COMP OP > < COMP OPERAND >
//   )
// | (
//     < TABLE AND COLUMN NAME >
//     ( < COMP OP > < COMP OPERAND > | < NULL OPERATION > )
//   )
// to reduce length to 1
void predicate() :
{}
{
  (
    comparableValue()
    < COMP_OP >
    compOperand()
  )
| (
    tableAndColumnName()
    (
      (
        < COMP_OP >
        compOperand()
      )
    | nullOperation()
    )
  )
}

void compOperand() :
{}
{
  comparableValue()
| tableAndColumnName()
}

// [ < TABLE NAME > < PERIOD > ] < COLUMN NAME >
// need 2 LOOKAHEAD
void tableAndColumnName() :
{}
{
  (
    LOOKAHEAD(2)
    tableName()
    < PERIOD >
  )?
  columnName()
}

void nullOperation() :
{}
{
  < IS >
  (
    < NOT_NULL >
  | < NULL >
  )
}

void insertColumnsAndSource() :
{}
{
  (
    columnNameList()
  )?
  valueList()
}

void valueList() :
{}
{
  < VALUES >
  < LEFT_PAREN >
  value()
  (
    < COMMA >
    value()
  )*
  < RIGHT_PAREN >
}

void value() :
{}
{
  < NULL >
| comparableValue()
}

void comparableValue() :
{}
{
  < INT_VALUE >
| < CHAR_STRING >
| < DATE_VALUE >
}

void tableElementList(Table t) :
{}
{
  < LEFT_PAREN >
  tableElement(t)
  (
    < COMMA >
    tableElement(t)
  )*
  < RIGHT_PAREN >
}

void tableElement(Table t) :
{}
{
  columnDefinition(t)
| primaryKeyConstraint(t)
| referentialConstraint(t)
}

void columnDefinition(Table t) :
{
  String s;
  Column col = new Column();
}
{
  s = columnName()
  {
    col.setName(s);
  }
  dataType(col)
  (
    < NOT_NULL >
    {
      col.setNotNull(true);
    }
  )?
  {
    t.addColumn(col);
  }
}

void primaryKeyConstraint(Table t) :
{
  ArrayList<String> l;
}
{
  < PRIMARY_KEY >
  l = columnNameList()
  {
    t.addPrimaryKey(l);
  }
}

void referentialConstraint(Table t) :
{
  String s; ArrayList<String> referencing, referenced;
}
{
  < FOREIGN_KEY >
  referencing = columnNameList()
  < REFERENCES >
  s = tableName()
  referenced = columnNameList()
  {
    t.addForeignKey(referencing, s, referenced);
  }
}

ArrayList<String> columnNameList() :
{
  String s;
  ArrayList<String> l = new ArrayList<String>();
}
{
  < LEFT_PAREN >
  s = columnName()
  {
    l.add(s);
  }
  (
    < COMMA >
    s = columnName()
    {
      l.add(s);
    }
  )*
  < RIGHT_PAREN >
  {
    return l;
  }
}

void dataType(Column col) :
{}
{
  (
    < INT >
    {
      col.setType(Column.INT);
    }
  | < CHAR >
    {
      col.setType(Column.CHAR);
    }
    < LEFT_PAREN >
    < INT_VALUE >
    {
      int len = Integer.parseInt(token.image);
      if(len < 1) {
        Message.print(Message.CHAR_LENGTH_ERROR);
        throw new ParseException();
      }
      col.setLength(len);
    }
    < RIGHT_PAREN >
  | < DATE >
    {
      col.setType(Column.DATE);
    }
  )
}

String tableName() :
{}
{
  < LEGAL_IDENTIFIER >
  {
    return token.image.toLowerCase();
  }
}

ArrayList<String> tableNameList() :
{}
{
  < ASTERISK >
  {
    return null;
  }
| {
    String s;
    ArrayList<String> l = new ArrayList<String>();
  }
  s = tableName()
  {
    l.add(s);
  }
  (
    < COMMA >
    s = tableName()
    {
      l.add(s);
    }
  )*
  {
    return l;
  }
}

String columnName() :
{}
{
  < LEGAL_IDENTIFIER >
  {
    return token.image.toLowerCase();
  }
}
